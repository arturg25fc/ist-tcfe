.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 100

* input coupling capacitor
Ci in2 base 11u

* bias circuit
R1 vcc base 120k
R2 base 0 20k

* gain stage
Q1 coll base emit BC547A
Rc vcc coll 0.8k
Re emit 0 100

* bypass capacitor
Cb emit 0 400u


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc 200

* output coupling capacitor
Co emit2 out 175u

* load
RL out 0 8

.op
.end

* -------------------------------------------------------------------------------
* A N A L Y S I S
.control

op

* o p e r a t i n g   p o i n t

let VBE1 = base - emit
let VCE1 = coll - emit
let VEB2 = emit2 - coll
let VEC2 = emit2


echo "op_TAB"
print all
print @q1[ib]
print @q1[ie]
print @q1[ic]
print @q2[ib]
print @q2[ie]
print @q2[ic]
echo "op_END"



* t r a n s i e n t   a n a l y s i s

tran 1e-5 1e-1

*plot v(in)
*plot v(out)

*hardcopy vint.ps v(in) 
*echo vint_FIG
*hardcopy vint.ps v(out) 
*echo voutt_FIG

*plot v(coll)
*hardcopy vcollt.ps v(coll) 
*echo vcollt_FIG


* f r e q u e n c y   a n a l y s i s

ac dec 10 10 100MEG

plot vdb(out)
plot vp(out)

hardcopy vdboutf.ps db(v(out))
echo vdboutf_FIG
hardcopy vphoutf.ps ph(v(out))
echo vphoutf_FIG


*plot vdb(coll)
*plot vp(coll)
*hardcopy vdbcollf.ps db(v(coll))
*echo vdbcollf_FIG
*hardcopy vphcollf.ps ph(v(coll))
*echo vphcollf_FIG


* -------------------------------------------------------------------------------
* I N P U T   I M P E D A N C E

let Zin = abs((-1)*v(in2)[30]/vin#branch[30])

echo "Zin_TAB"
print Zin
echo "Zin_END"

*plot abs(v(in2)/vin#branch/(-1000))
*print v(in2)/vin#branch/(-1000)
*plot abs(v(in2)[40]/vin#branch[40]/(-1000))
*print (-1)*v(in2)[30]/vin#branch[30]



* -------------------------------------------------------------------------------
* M E R I T


* g a i n 

let abs_out = abs(v(out))
meas ac max_out MAX abs_out from=10 to=100MEG

let abs_in = abs(v(in))
meas ac max_in MAX abs_in from=10 to=100MEG

let gain = max_out/max_in



* c u t - o f f   f r e q u e n c i e s   +   b a n d w i d t h  

meas ac gaindb MAX vdb(out) from=10 to=100MEG
let gaindb_cutoff  = gaindb-3

meas ac cutofflow WHEN vdb(out)=gaindb_cutoff
meas ac cutoffhigh WHEN vdb(out)=gaindb_cutoff FALL=LAST

let bandwidth = cutoffhigh-cutofflow

* c o s t

let cost = (0.1 + 120 + 20 + 0.8 + 0.1 + 0.200) + (11 + 400 + 175) + (0.2)


* m e r i t

let merit = gain*bandwidth/(cost*cutofflow)


* t a b l e

echo "merit_TAB"
print gain
print gaindb
print cutofflow
print cutoffhigh
print bandwidth
print cost
print merit
echo "merit_END"



.endc
